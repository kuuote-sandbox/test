pkgname='neovim-git'
pkgver=17134.0f2d37f11
pkgrel=1
pkgdesc='Fork of Vim aiming to improve user experience, plugins, and GUIs.'
arch=('i686' 'x86_64')
url='https://neovim.io'
license=('custom:neovim')
depends=()
makedepends=('cmake' 'unzip')
optdepends=('python2-neovim: for Python 2 plugin support (see :help nvim-python)'
            'python-neovim: for Python 3 plugin support (see :help nvim-python)'
            'xclip: for clipboard support (or xsel) (see :help nvim-clipboard)'
            'xsel: for clipboard support (or xclip) (see :help nvim-clipboard)')
source=("git://github.com/neovim/neovim")
md5sums=('SKIP')

pkgver() {
    cd "${srcdir}/neovim"
    echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd "${srcdir}/neovim"
  make -j$(nproc)
}

check() {
  cd "${srcdir}/neovim/build"
  ./bin/nvim --version
  ./bin/nvim --headless -u NONE -i NONE -c ':quit'
}

package() {
  cd "${srcdir}/neovim/build"
  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}/neovim"/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set sw=2 sts=2 et:
